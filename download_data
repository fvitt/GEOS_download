#!/bin/csh

echo "BEGIN DOWNLOAD"

set argnum = 0

set base_filename = "GEOS.fp.asm"
set remote_basedir = "/fp/das"
set local_basedir = "/glade/scratch/$USER/GEOS"

set server = https://portal.nccs.nasa.gov/datashare/gmao_ops/pub

foreach arg ($argv)
  @ argnum++
  @ argnump = $argnum + 1
  switch ($arg)
   case "-year":
     set year = "$argv[$argnump]"; breaksw
   case "-month":
     set month = "$argv[$argnump]"; breaksw
   case "-day":
     set day = "$argv[$argnump]"; breaksw
   case "-hour":
     set hour = "$argv[$argnump]"; breaksw
   case "-remote_basedir":
     set remote_basedir = "$argv[$argnump]"; breaksw
   case "-local_basedir":
     set local_basedir = "$argv[$argnump]"; breaksw
   case "-base_filename":
     set base_filename = "$argv[$argnump]"; breaksw
  endsw

end

set yyyymmdd_date = ${year}${month}${day}

set local_dir = "${local_basedir}/Y${year}/M${month}/D${day}"
set remote_dir = "${remote_basedir}/Y${year}/M${month}/D${day}"
if (${?hour}) then
  set remote_dir = "${remote_dir}/H${hour}"
  set local_dir = "${local_dir}/H${hour}"
endif

set set_cmds = "set cmd:fail-exit true; set net:max-retries 5"
set lftp_opts = "${set_cmds}; mirror -c -r -p --parallel=4 --no-symlinks -L --verbose=1 -I "

set loop_cnt = 0
set file_tot = 80

set loop_lim = 2
set sleep_dt = 60

echo "mkdir -p $local_dir"
mkdir -p $local_dir

while ( $loop_cnt < $loop_lim )
    echo "loop_cnt = $loop_cnt"
    date
    set glob = "${base_filename}.inst3_3d_asm_Nv*.nc4"
    echo "/usr/bin/lftp -c $lftp_opts $glob $server$remote_dir $local_dir"
    /usr/bin/lftp -c "$lftp_opts $glob $server$remote_dir $local_dir"

    set glob = "${base_filename}.tavg1_2d_flx_Nx*.nc4"
    echo "/usr/bin/lftp -c $lftp_opts $glob $server$remote_dir $local_dir"
    /usr/bin/lftp -c "$lftp_opts $glob $server$remote_dir $local_dir"

    set glob = "${base_filename}.tavg1_2d_lnd_Nx*.nc4"
    echo "/usr/bin/lftp -c $lftp_opts $glob $server$remote_dir $local_dir"
    /usr/bin/lftp -c "$lftp_opts $glob $server$remote_dir $local_dir"

    set glob = "${base_filename}.tavg1_2d_rad_Nx*.nc4"
    echo "/usr/bin/lftp -c $lftp_opts $glob $server$remote_dir $local_dir"
    /usr/bin/lftp -c "$lftp_opts $glob $server$remote_dir $local_dir"

    date

    set xfer_file_cnt = `ls $local_dir/*${yyyymmdd_date}_* | wc -l`
    echo " number of files downloaded: ${xfer_file_cnt} "
    if( $xfer_file_cnt == $file_tot ) then
      echo "DOWNLOAD SUCCESSFUL"
      exit 0
    endif
    @ loop_cnt++
    if ( $loop_cnt < $loop_lim ) then
      sleep $sleep_dt
    endif
end

echo "DOWNLOAD FAILED"
exit -1

